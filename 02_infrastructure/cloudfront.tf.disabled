# Theoretische Implementierung eines CDN mit ClouDFront
# Nicht umsetzbar, da der AWS Support das Konto nicht freischaltet (siehe Anhang im Bericht)
# Konnte daher nicht auf Korrektheit geprüft werden

resource "aws_cloudfront_distribution" "s3_website_distribution" {
  origin {
    domain_name = aws_s3_bucket_website_configuration.website_config.website_endpoint
    origin_id   = "S3-Website-${var.bucket_name}"

    custom_origin_config {
      http_port              = 80
      https_port             = 443
      origin_protocol_policy = "http-only"
      origin_ssl_protocols   = ["TLSv1.2"]
    }
  }

  enabled             = true
  is_ipv6_enabled     = true
  default_root_object = "index.html"

  default_cache_behavior {
    allowed_methods  = ["GET", "HEAD"]
    cached_methods   = ["GET", "HEAD"]
    target_origin_id = "S3-Website-${var.bucket_name}"

    viewer_protocol_policy = "redirect-to-https"

    forwarded_values {
      query_string = false
      cookies {
        forward = "none"
      }
    }

    min_ttl     = 0
    default_ttl = 3600
    max_ttl     = 86400
  }

  restrictions {
    geo_restriction {
      restriction_type = "none"
    }
  }

  viewer_certificate {
    cloudfront_default_certificate = true
  }

  tags = {
    Name        = "CloudFront Distribution für ${var.bucket_name}"
    Environment = "Production"
  }
}

output "cloudfront_domain_name" {
  description = "Ausgabe der Domain"
  value       = try(aws_cloudfront_distribution.s3_website_distribution.domain_name, "Nicht verfügbar")
}